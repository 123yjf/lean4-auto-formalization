import Mathlib.Algebra.Ring.Associated
import Mathlib.Tactic.Linarith
import Mathlib.Tactic.LinearCombination

namespace Imo2001P6

variable {K L M N : ℤ}


theorem imo_2001_p6
    (hN : 0 < N) (hMN : N < M) (hML : M < L) (hLK : L < K)
    (h : K * M + L * N = (K + L - M + N) * (-K + L + M + N)) :
    ¬ Prime (K * L + M * N) := by
  intro h0
  have hK : 0 < K := by omega
  have hL : 0 < L := by omega
  have hM : 0 < M := by omega
  
  have dvd_mul : K * M + L * N ∣ (K * L + M * N) * (K * N + L * M) := by
    use L ^ 2 + L * N + N ^ 2
    linear_combination L * N * h
  
  obtain h1 | h2 := h0.left_dvd_or_dvd_right_of_dvd_mul dvd_mul
  · have aux : 0 < K * M + L * N := by nlinarith only [hK, hL, hM, hN]
    have : K * L + M * N ≤ K * M + L * N := Int.le_of_dvd aux h1
    nlinarith only [hLK, hML, hMN, h, this]
  · have aux : 0 < K * N + L * M := by nlinarith only [hK, hL, hM, hN]
    have : K * M + L * N ≤ K * N + L * M := Int.le_of_dvd aux h2
    nlinarith only [hLK, hMN, h, this]

end Imo2001P6






































































































































































































































































